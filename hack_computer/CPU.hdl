/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction.
 * 
 * Author: Naga Kandasamy
 * Date created: October 6, 2020
 * 
 * Student name(s): 
 * Date modified: 
 * 
 * Notes: This project is part of www.nand2tetris.org and the book "The Elements of Computing Systems" by Nisan and Schocken, MIT Press.
 */

CHIP CPU {
    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=notInst15);
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=mux16out);
    Or(a=instruction[5], b=notInst15, out=loadedA);
    ARegister(in=mux16out, load=loadedA, out=ARegOut, out[0..14]=addressM);
    And(a=instruction[15], b=instruction[4], out=loadedD);
    DRegister(in=aluOut, load=loadedD, out=DRegOut);
    And(a=instruction[15], b=instruction[12], out=andInst1512);
    Mux16(a=ARegOut, b=inM, sel=andInst1512, out=ARegInMor);
    ALU(x=DRegOut, y=ARegInMor, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=aluZrOut, ng=aluNgOut);
    And(a=instruction[15], b=instruction[3], out=writeM);
    And(a=instruction[15], b=instruction[2], out=andInstruction152);
    And(a=andInstruction152, b=aluNgOut, out=jlt);
    And(a=instruction[15], b=instruction[1], out=andInstruction151);
    And(a=andInstruction151, b=aluzrOut, out=jzr);
    And(a=instruction[15], b=instruction[0], out=andInstruction150);
    Not(in=aluzrOut, out=notaluzrOut);
    Not(in=aluNgOut, out=notaluNgOut);
    And(a=andInstruction150, b=notaluzrOut, out=jgt1);
    And(a=jgt1, b=notaluNgOut, out=jgt);
    Or(a=jlt, b=jzr, out=loadedPC);
    Or(a=loadedPC, b=jgt, out=loadPC);
    PC(in=ARegOut, load=loadPC, inc=true, reset=reset, out[0..14]=pc);
}